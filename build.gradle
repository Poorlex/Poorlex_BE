plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id "io.sentry.jvm.gradle" version "4.3.1"
}

group = 'com.poorlex'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

def getSentryToken() {
    final Properties properties = new Properties()
    final Reader reader = new FileReader(project.rootProject.file('src/main/resources/secure/application-dev-info.yml'))
    properties.load(reader)
    return properties.getProperty("sentry-token")
}

sentry {
    includeSourceContext = true
    org = "poorlex"
    projectName = "java-spring-boot"
    authToken = getSentryToken()
}

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    //SECURITY
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    //Oauth
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    //BATCH
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    testImplementation 'org.springframework.batch:spring-batch-test'

    //AWS
    implementation 'com.amazonaws:aws-java-sdk:1.11.163'
    implementation 'javax.xml.bind:jaxb-api:2.3.0'

    //LOMBOK
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //DB - H2
    runtimeOnly 'com.h2database:h2'

    //DB - MySQL
    runtimeOnly 'com.mysql:mysql-connector-j'

    //TOKEN
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.68'

    //SWAGGER
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

    //REST-DOCS
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc')

    //SENTRY
    implementation 'io.sentry:sentry-spring-boot-starter-jakarta:7.6.0'
    implementation 'com.github.maricn:logback-slack-appender:1.4.0'

    //QUERY DSL
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    configurations 'asciidoctorExt'
    baseDirFollowsSourceFile()
    sourceDir 'src/docs/asciidocs'
    dependsOn test
}

asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
    println("delete finished")
}

tasks.register('copyDocument', Copy) {
    dependsOn asciidoctor

    from file("build/docs/asciidoc/index.html")
    into file("src/main/resources/static/docs")
}

bootJar {
    dependsOn copyDocument
}

tasks.named('bootBuildImage') {
    builder = 'paketobuildpacks/builder-jammy-base:latest'
}

tasks.named('test') {
    useJUnitPlatform()
}
